apply plugin: 'java'
apply plugin: 'maven'

version = '1.0'
group = 'ipng-converter'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.jcraft:jzlib:1.1.2'
}


jar {
    manifest {
        attributes 'Implementation-Title': 'iPNG Converter',
            'Implementation-Version': version,
            'Built-Date': new Date(),
            'Built-JDK': System.getProperty('java.version'),
            'Built-Gradle': gradle.gradleVersion
    }
}

install.repositories.mavenInstaller {
    pom.project(pomConfiguration)
}

uploadArchives {
    repositories.mavenDeployer {
        name = 'mavenCentralReleaseDeployer'
        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
            authentication(userName: System.properties['mavenCentralUsername'], password: System.properties['mavenCentralPassword'])
            releases(updatePolicy: 'always')
            snapshots(updatePolicy: 'always')
        }
        pom.project(pomConfiguration)
    }
}

task writePom << {
    pom {
        project { pomConfiguration }.writeTo("$buildDir/libs/${project.name}-${version}.pom")
    }
}

def getPomConfiguration() {
    return {
        name 'iPngConverter'
        packaging 'jar'
        description 'iPhone CgIB Optimized PNG Converter'
        url 'https://github.com/viphe/ipng-converter'
        licenses {
            license {
                name 'CC0 1.0 Universal (CC0 1.0) - Public Domain Dedication'
                url 'http://creativecommons.org/publicdomain/zero/1.0/'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'viphe'
                name 'Philippe Vosges'
                email 'philippe.vosges@gmail.com'
            }
        }
        scm {
            connection 'scm:https://github.com/viphe/ipng-converter'
            developerConnection 'scm:git@github.com:viphe/ipng-converter.git'
            url 'https://github.com/viphe/ipng-converter'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.7'
}
